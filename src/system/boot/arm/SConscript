'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import SCons

Import(['env'])

builddir = env['BUILDDIR']
kerneldir = env['PEDIGREE_BUILD_KERNEL']
objname = os.path.join(builddir, 'bootloader')
uimage = os.path.join(builddir, 'uImage.bin')
kernel = os.path.join(kerneldir, 'kernel')
initrd = os.path.join(env['PEDIGREE_BUILD_BASE'], 'initrd.tar')

env.Alias('kernel', objname)
env.Alias('kernel', uimage)
env.Depends(objname, kernel)

env_clone = env.Clone()

files = [
    'Elf32.cc',
    'arm_support.s',
    'support.cc',
]

if env['arm_beagle']:
    files += ['main_beagle.cc']

env_clone.FileAsHeader('autogen.h', kernel)

env_clone['CPPPATH'] = [env.File('autogen.h').get_dir()]
env_clone.Program(objname, files, LINKFLAGS='-nostdlib -T$LSCRIPT',
                  LSCRIPT=File('#src/system/boot/arm/link-arm-beagle.ld'))

if env['arm_beagle']:
    if env['MKIMAGE'] is None:
        raise SCons.Errors.UserError('mkimage is not available')

    bootloader_raw = objname + '.flt'

    # Set up the uboot image.
    kernelType = "linux"
    loadAddr = "0x87000000"
    env.Command(bootloader_raw, objname, '$OBJCOPY -O binary $SOURCE $TARGET')
    env.Command(uimage, bootloader_raw, '$MKIMAGE -A arm -O linux -T kernel '
        '-C none -a 0x87000000 -e 0x87000000 -n pedigree -d $SOURCE $TARGET && '
        'cp $TARGET $uimage_target')
