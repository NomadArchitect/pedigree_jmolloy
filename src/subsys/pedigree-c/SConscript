'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace

Import(['env'])

env_clone = env.Clone()

builddir = "#" + env["PEDIGREE_BUILD_BASE"]
kerneldir = "#" + env["PEDIGREE_BUILD_KERNEL"]
moduledir = "#" + env["PEDIGREE_BUILD_MODULES"]
subsysdir = "#" + env["PEDIGREE_BUILD_SUBSYS"]

pedigree_c_objname = os.path.join(subsysdir, 'pedigree-c.o')
libpedigree_c_objname = os.path.join(builddir, 'libpedigree-c.a')
libpedigree_c_shobjname = os.path.join(builddir, 'libpedigree-c.so')

env.Alias("subsys", pedigree_c_objname)
env.Alias("subsys", libpedigree_c_objname)
env.Alias("subsys", libpedigree_c_shobjname)

kernel_module_files = Glob("*.cc")

libpedigree_c_files = Glob("*.c")

# To include a new directory for includes, just add it to the list
include_dirs = [
    './include',
    '#/src/modules',
    '#/src/system/include',
    '#/src/modules/system',
	'#/src/subsys/posix/include',
    '.'
]

if 'STATIC_DRIVERS' in env['CPPDEFINES']:
    libraries = []
else:
    libraries = ['gcc']

env_clone['CPPPATH'] = include_dirs
env_clone['LIBS'] = libraries
env_clone['LIBPATH'] = [builddir, env['LIBGCC']]

final_output = pedigree_c_objname
pedigree_c_objname = '%s.debug' % (pedigree_c_objname,)

if "STATIC_DRIVERS" in env['CPPDEFINES']:
    env_clone.Program(pedigree_c_objname, kernel_module_files,
        LINKFLAGS='-nostdlib -Wl,-r -T $LSCRIPT',
        LSCRIPT=File("#src/modules/link_static.ld"))
    env_clone.Command(final_output, pedigree_c_objname,
        action="%s -d -o $TARGET $SOURCE" % env['STRIP'])
else:
    print [x.path for x in kernel_module_files]
    env_clone.Program(pedigree_c_objname, kernel_module_files,
        LINKFLAGS='-nostdlib -r -T $LSCRIPT',
        LSCRIPT=File("#src/modules/link.ld"))
    env_clone.Command(final_output, pedigree_c_objname,
        action="%s -d -o $TARGET $SOURCE" % env['STRIP'])

# -fPIC for libpedigree-c
glueFlags = " -fPIC -DUSE_PIC_SYSCALLS "

libs_clone = env.Clone()

libs_clone['CFLAGS'] = buildutils.userspace.fixFlags(env, libs_clone['CFLAGS'])
libs_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(env, libs_clone['CXXFLAGS'])
libs_clone['CFLAGS'] += glueFlags
libs_clone['CXXFLAGS'] += glueFlags

libs_clone.StaticLibrary(libpedigree_c_objname, libpedigree_c_files)
libs_clone.SharedLibrary(libpedigree_c_shobjname, libpedigree_c_files, LINKFLAGS='')

env.Depends(libpedigree_c_shobjname, "crt")
