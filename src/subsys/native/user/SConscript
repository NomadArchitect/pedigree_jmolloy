####################################
# SCons build system for Pedigree
## Tyler Kennedy (AKA Linuxhq AKA TkTech)
####################################

import os
Import(['env'])

tmp = env.Clone()

# Build directories
builddir = "#" + env["PEDIGREE_BUILD_BASE"]
kerneldir = "#" + env["PEDIGREE_BUILD_KERNEL"]
moduledir = "#" + env["PEDIGREE_BUILD_MODULES"]
subsysdir = "#" + env["PEDIGREE_BUILD_SUBSYS"]

# We output a single library, which applications link against to gain access to
# the implementation of the native subsystem classes.
native_userlib_objname = builddir + '/libpedigree.a'
native_userlib_shobjname = builddir + '/libpedigree.so'

env.Alias("libs", native_userlib_objname)
env.Alias("libs", native_userlib_shobjname)

# To include a new subdirectory just add to the list.
subdirs = [

]

lib_files = [
    Glob("*.cc"),
    Glob("config/*.cc"),
    Glob("graphics/*.cc"),
    Glob("input/*.cc"),
    Glob("ipc/*.cc")
]

# To include a new directory for includes, just add it to the list
include = [
    '../include',
    '#/src/subsys',
    '#/src/system/include',
    '#/src/modules/system',
    '#/src/subsys/posix/include',
    '#/images/local/support/gcc/include/c++/4.5.1',
    '#/images/local/support/gcc/include/c++/4.5.1/i686-pedigree',
    '.'
]

# To add a library add to the list below
libraries = [
    'gcc'
]

# To add a library path add to the list below
libpaths = [
    builddir,
    env['LIBGCC'],
]

####################################
SConscript([os.path.join(i, 'SConscript') for i in subdirs],exports = ['tmp'])
tmp['CPPPATH'] = include
tmp['LIBS'] = libraries
tmp['LIBPATH'] = libpaths
tmp['CXXFLAGS'] = tmp['CXXFLAGS'].replace('-nostdinc', '')

# -fPIC for libpedigree
glueFlags = " -fno-exceptions -fPIC -DUSE_PIC_SYSCALLS "

tmp['CFLAGS'] = tmp['CFLAGS'].replace("mcmodel=kernel", "mcmodel=small")
tmp['CXXFLAGS'] = tmp['CXXFLAGS'].replace("mcmodel=kernel", "mcmodel=small")
tmp['CFLAGS'] += glueFlags
tmp['CXXFLAGS'] += glueFlags

tmp.Library(native_userlib_objname, lib_files)
tmp.SharedLibrary(native_userlib_shobjname, lib_files, LINKFLAGS='')
