'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

Import(['env'])

cdi_drivers = env['cdi_driver_list']

# Meh. cdi-osdep.h is in both locations, this isn't ideal.
# TODO: Fix me!
include_dirs = [
    '#src/system/include',
    '#src/modules/drivers',
    '#src/modules/drivers/common/cdi/include',
    '#src/modules/drivers/cdi/include'
]

libraries = [
    'gcc'
]

# For each driver, handle its compilation
for driver in cdi_drivers:
    env_clone = env.Clone()

    directory = os.path.join("cdi", driver)
    
    objname = os.path.join('#' + env["PEDIGREE_BUILD_DRIVERS"], driver + '.o')
    
    driverFiles = [Glob(os.path.join(directory, '*.c'))]
    
    env_clone['LIBS'] = libraries
    env_clone['CPPPATH'] = include_dirs
    env_clone['CPPPATH'] += [os.path.join(directory, 'include')]

    # TOOD: do a warning fixup in CDI
    env_clone['CFLAGS'] = env_clone['CFLAGS'].replace('-Werror', '')
    env_clone['CXXFLAGS'] = env_clone['CXXFLAGS'].replace('-Werror', '')

    env_clone.Program(objname, driverFiles,
        LINKFLAGS='-nostdlib -Wl,-r -T $LSCRIPT',
        LSCRIPT=File("#src/modules/link.ld"))

    env.Alias("drivers", objname)

