#!/usr/bin/env python2.7
'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

####################################
# SCons build system for Pedigree
## Tyler Kennedy (AKA Linuxhq AKA TkTech)
####################################

Import(['env'])

import os

import buildutils.userspace

# Temporary copy of the global environment so we can fiddle with flags
tmpEnvironment = env.Clone()

# Change to then name of this module
builddir = "#" + env["PEDIGREE_BUILD_BASE"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]
objname = builddir + '/libSDL.a'
sobjname = builddir + '/libSDL.so'

# To include a new subdirectory just add to the list.
subdirs = [
    
]

# To include new files just add to the list, or Glob("*.cpp")
files = [
    Glob('src/*.c'),
    Glob('src/hermes/*.asm'),
    Glob('src/audio/*.c'),
    Glob('src/cdrom/*.c'),
    Glob('src/cpuinfo/*.c'),
    Glob('src/events/*.c'),
    Glob('src/file/*.c'),
    Glob('src/joystick/*.c'),
    Glob('src/stdlib/*.c'),
    Glob('src/thread/*.c'),
    Glob('src/timer/*.c'),
    Glob('src/video/*.c'),
    Glob('src/audio/dummy/*.c'),
    Glob('src/video/pedigree/*.c'),
    Glob('src/video/pedigree/*.cc'),
    Glob('src/joystick/dummy/*.c'),
    Glob('src/cdrom/dummy/*.c'),
    Glob('src/thread/pthread/*.c'),
    Glob('src/timer/unix/*.c'),
    Glob('src/loadso/dlopen/*.c'),
]

# To include a new directory for includes, just add it to the list
include = [
    './include',
    '#/src/subsys/posix/include',
    '#/src/subsys/native/include',
    '#/src/user/libraries/libui/include',
    '#/images/local/include',
    '#/images/local/include/pixman-1',
    '#/images/local/include/freetype2',
]

cpp_base = '#/images/local/support/gcc/include/c++/%s' % (env['COMPILER_VERSION'],)
cppinclude = include + [
    cpp_base,
    '%s/%s' % (cpp_base, env['COMPILER_TARGET']),
]

# To add a library add to the list below
libraries = [
    'gcc',
    'c',
    'm',
    'ui',
    'pedigree',
    'pedigree-c',
    'pthread',
    'stdc++',
    'cairo',
    'pixman-1',
    'fontconfig',
    'expat',
    'freetype',
    'png',
    'z',
    'iconv',
]

# To add a library path add to the list below
libpaths = [
    '#',
    imagesdir + 'libraries',
    builddir,
    builddir + '/libs',
]

tmpEnvironment['CFLAGS'] = buildutils.userspace.fixFlags(env, tmpEnvironment['CFLAGS'])
tmpEnvironment['CXXFLAGS'] = buildutils.userspace.fixFlags(env, tmpEnvironment['CXXFLAGS'])
tmpEnvironment['CPPPATH'] = cppinclude
tmpEnvironment['LIBS'] = libraries
tmpEnvironment['LIBPATH'] = libpaths
tmpEnvironment['CPPDEFINES'] += ['PEDIGREE']
tmpEnvironment['ASFLAGS'] += ' -I' + env.Dir('#src/lgpl/SDL-1.2.14/src/hermes').path + '/ '

if env['ARCH_TARGET'] in ['X86', 'X64']:
    tmpEnvironment.SharedLibrary(sobjname, files, LINKFLAGS="")
tmpEnvironment.Library(objname, files, LINKFLAGS="")

env.Alias(sobjname, 'libs')
env.Alias(objname, 'libs')

SConscript([os.path.join(i, 'SConscript') for i in subdirs],exports = ['env'])
