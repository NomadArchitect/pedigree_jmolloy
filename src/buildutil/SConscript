'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''


import os

Import(['host_env'])

env = host_env
conf = Configure(env)

KERNEL_DIR = env.Dir('../system/kernel')

# Distinguish build system objects from those built for the target, always.
env['OBJSUFFIX'] = '.build.o'

subdirs = [
    'ext2img',
    'testsuite',
]

# Build utility libraries.
header_paths = [
    env.Dir('#src/modules/system'),
    env.Dir('#src/system/include'),
]
libutility_sources = [
    os.path.join('utilities', 'String.cc'),
    os.path.join('utilities', 'List.cc'),
    os.path.join('utilities', 'Vector.cc'),
    os.path.join('utilities', 'Tree.cc'),
    os.path.join('utilities', 'ExtensibleBitmap.cc'),
    os.path.join('utilities', 'ObjectPool.cc'),
    os.path.join('core', 'lib', 'string.c'),
    os.path.join('core', 'lib', 'memory.c'),
    os.path.join('core', 'lib', 'vsprintf.c'),
    'Log.cc',
]
defines = [
    'HOSTED', 'UTILITY_LINUX', 'LITTLE_ENDIAN', 'DONT_LOG_TO_SERIAL',
]

utility_env = env.Clone()
utility_env.Append(**{
    'CPPPATH': header_paths,
    'CPPDEFINES': defines,
})
utility_env.MergeFlags({
    'CFLAGS': ['-g3', '-ggdb'],
    'CXXFLAGS': ['-std=c++11', '-g3', '-ggdb'],
    'CFLAGS': ['-std=gnu99', '-g3', '-ggdb'],
})

env['LIBUTILITY'] = utility_env.Library('utility',
    ['shim.cc'] + [KERNEL_DIR.File(f) for f in libutility_sources],
    CCFLAGS=['-O2'])

# Build a version of libutility that has the necessary coverage pieces, such
# that we can get code coverage analysis where actual .cc files hold the code
# being tested.
utility_env['OBJSUFFIX'] = '.build.coverage.o'
env['LIBUTILITY_COVERAGE'] = utility_env.Library('utility_coverage',
    ['shim.cc'] + [KERNEL_DIR.File(f) for f in libutility_sources],
    CCFLAGS=utility_env['COVERAGE_CCFLAGS'])

for subdir in subdirs:
    SConscript(os.path.join(subdir, 'SConscript'), exports=['env'])
