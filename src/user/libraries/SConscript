####################################
# SCons build system for Pedigree
## Tyler Kennedy (AKA Linuxhq AKA TkTech)
####################################

import os.path
Import(['env'])

tmp = env.Clone()

####### END-USER LIBRARY BUILDS #######

# Cleans a set of flags so we can build proper libraries rather than
# freestanding stuff.
def fixFlags(flags):
    flags = flags.replace('-nostdinc', '')
    flags = flags.replace('-ffreestanding', '')
    flags = flags.replace('-nostdlib', '')
    flags = flags.replace('-fno-builtin', '')
    if env['ARCH_TARGET'] in ['X86', 'X64']:
        flags = flags.replace('-mno-mmx', '')
        flags = flags.replace('-mno-sse', '')
        flags = flags.replace('-fno-exceptions', '')
        flags = flags.replace('-fno-rtti', '')
        flags += ' -msse2 -mfpmath=sse '
    if env['ARCH_TARGET'] == 'X64':
        flags = flags.replace('-mcmodel=kernel', '-mcmodel=small')
        flags = flags.replace('-mno-red-zone', '')
        flags = flags.replace('-mno-mmx', '')
        flags = flags.replace('-mno-sse', '')
        flags += ' -msse2 -mfpmath=sse '
        print flags
    if env['ARCH_TARGET'] == 'PPC':
        flags += ' -U__svr4__ '
    return flags

libsbase        = 'libraries/'
appoutputdir    = "#" + env["PEDIGREE_BUILD_APPS"]
imagesdir       = env["PEDIGREE_IMAGES_DIR"]
builddir        = "#" + env["PEDIGREE_BUILD_BASE"]

# Libraries to compile
libs = [
    'libui'
]

# Subsystem for each set of libraries
subsys = {
    'libui' : 'native'
}

tmp['CFLAGS'] = fixFlags(env['CFLAGS'])
tmp['CXXFLAGS'] = fixFlags(env['CXXFLAGS'])
tmp['CPPDEFINES'] += ['PEDIGREE', '__PEDIGREE__']
tmp['LIBS'] = []
tmp['LIBPATH'] = [
    builddir,
]
tmp['CPPPATH'] = []

for i in libs:
    e = tmp.Clone()
    
    # Determine the subsystem and include directories for this library
    incpath = []
    s = subsys[i]
    if s == 'native':
        incpath += ['#/src/subsys/native/include',
                    '#/images/local/support/gcc/include/c++/4.5.1',
                    '#/images/local/support/gcc/include/c++/4.5.1/i686-pedigree',
                    ]
    else:
        print "** Library with subsys '%s' is not buildable - fix src/user/libraries/SConscript!" % (s)
    
    base = "#/src/user/libraries/%s" % (i)

    # Handle extra include directories within the library
    if(os.path.exists(Dir(base).abspath + '/include')):
        incpath += [base + '/include']
        
    e['CPPPATH'] = incpath

    output = os.path.join(builddir, 'libs', '%s.a' % (i,))
    shoutput = os.path.join(builddir, 'libs', '%s.so' % (i,))
    files = [Glob(base + '/src/*.c'), Glob(base + '/src/*.cc')]

    e.Library(output, files)
    e.SharedLibrary(shoutput, files, LINKFLAGS='')

    env.Alias("libs", output)
    env.Alias("libs", shoutput)

    # XXX: We have an unfortunate dependency on libc.
    libc_objname = builddir + '/libc.so'
    env.Depends(shoutput, libc_objname)

